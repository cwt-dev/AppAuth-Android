apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

apply from: '../config/android-common.gradle'

group = "net.openid"
version = rootProject.versionName

android.defaultConfig.project.archivesBaseName = 'cwt-appauth'

// use a separate build type for unit tests, so that we can configure the manifest placeholder
// without interfering with other builds.
android.buildTypes {
    forTests {
        initWith debug
        manifestPlaceholders = [
            'appAuthRedirectScheme': 'net.openid.appauth.test'
        ]
    }
}

android.testBuildType "forTests"

dependencies {
    api "com.android.support:customtabs:${rootProject.supportLibVersion}"
    apply from: '../config/testdeps.gradle', to:it
}

apply from: '../config/style.gradle'
apply from: '../config/coverage.gradle'
apply from: '../config/javadoc.gradle'

artifacts {
    archives sourcesJar
    archives javadocJar
}

//ref:
// https://github.com/jfrog/project-examples/blob/master/gradle-examples/gradle-android-example/build.gradle
// https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
// https://jeroenmols.com/blog/2015/08/06/artifactory/
artifactory {
    contextUrl = 'http://art.mobimate.local/artifactory'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = 'libs-release-local'
            username = System.getenv("artifactory_user") // ${artifactory_user} // The publisher user name
            password = System.getenv("artifactory_password") // ${artifactory_password} // The publisher password
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    pkg {
        repo = "net.openid"
        name = "appauth"
        desc = "AppAuth for Android"
        userOrg = "openid"
        licenses = ["Apache-2.0"]
        websiteUrl = "https://openid.github.io/AppAuth-Android/"
        issueTrackerUrl = "https://github.com/openid/AppAuth-Android/issues"
        vcsUrl = "https://github.com/openid/AppAuth-Android.git"
        githubRepo = 'bintray/gradle-bintray-plugin'

        version {
            name = rootProject.versionName
            desc = "AppAuth for Android"
            released = rootProject.versionDate
            vcsTag = rootProject.versionName
        }
    }
    configurations = ["archives"]
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'AppAuth for Android'
                description = 'OAuth2 / OpenID Connect client for Android apps'
                url 'https://github.com/openid/AppAuth-Android'
                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "iainmcgin"
                        name "Iain McGinniss"
                        email "iain.mcginniss@wework.com"
                    }
                }

                scm {
                    connection "https://github.com/openid/AppAuth-Android.git"
                    developerConnection "https://github.com/openid/AppAuth-Android.git"
                    url 'https://github.com/openid/AppAuth-Android'
                }
            }
        }
    }
}
